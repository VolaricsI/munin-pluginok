#!/usr/bin/perl -w
#
# xmlrpc based munin plugin for monitoring rTorrent's upload/download speed
# prerequisites:
#  - rTorrent 0.7.5 or newer compiled with --with-xmlrpc-c
# check http://libtorrent.rakshasa.no/wiki/RTorrentXMLRPCGuide for further informations
#
# written by Gabor Hudiczius
# web: http://rtwi.jmk.hu/wiki/rTorrentOMeter
# email: ghudiczius@gmail.com
#
#
# obsolete - use "rtom__spdd" for more accurate graphs
#
#
# Parameters:
#
#	config		required
#
#
# Configurable variables
#
#	src			"socket" when using scgi_socket, or anything else when using scgi_port
#	socket		rTorrent's rpc socket (scgi_local)	- using scgi_local	- needed, when "src" is set to "socket"
#	ip			rTorrent's ip address				- using scgi_port	- needed, when "src" is NOT set to "socket"
#	port		rTorrent's scgi port (scgi_port)	- using scgi_port	- needed, when "src" is NOT set to "socket"
#
#	diff		"yes" for using bits per second for upload and Bytes per second for download, or anything else for using Bytes per second for both
#
#
# Configuration example
#
#	[rtom__spd]
#	user username
#	env.src socket
#	env.socket /home/username/torrent/.socket/rpc.socket
#
#	[rtom__spd]
#	env.ip 127.0.0.1
#	env.port 5000
#
#
#%# family=auto

my $src 		= $ENV{"src"} || "";
my $ip			= $ENV{"ip"} || "127.0.0.1";
my $port		= $ENV{"port"} || 5000;
my $socket		= $ENV{"socket"} || "";
my $diff		= $ENV{"diff"} || 0;

#if ( $0 =~ /^(?:|.*\/)rtom_(.*)_spd$/ ) {
#    $user = " for ${1}";
#} else {
    $user = "";
#}

if ( $ARGV[0] and $ARGV[0] eq "autoconf" ) {
	exit 1;
}

if ( $ARGV[0] and $ARGV[0] eq "config" ) {
	print "graph_order down up\n";
	print "graph_title rTorrent speeds${user} (obsolete)\n";
	print "graph_args --base 1024 --lower-limit 0\n";
	print "graph_vlabel Bytes per \${graph_period}\n";
	print "graph_category rTorrent${user}\n";
	print "down.label Download (Bytes)\n";
	print "down.info Download speed in Bytes per \${graph_period}\n";
	print "down.type GAUGE\n";
	print "down.min 0\n";
	print "down.draw AREA\n";
	if ( $diff eq "yes" ) {
		print "up.label Upload (bits)\n";
		print "up.info Upload speed in bits per \${graph_period}\n";
		print "up.cdef up,8,*\n";
	} else {
		print "up.label Upload (Bytes)\n";
		print "up.info Upload speed in Bytes per \${graph_period}\n";
	}
	print "up.type GAUGE\n";
	print "up.min 0\n";
	print "up.draw LINE2\n";
	exit 0;
}

use IO::Socket;

my $pattern	= qr/<value><(int|i4|i8|ex\.i8)>(\d+)<\/(int|i4|i8|ex\.i8)><\/value>/;

my $line	= "<?xml version=\"1.0\" encoding=\"utf-8\"?><methodCall><methodName>d.multicall</methodName><params><param><value><string>default</string></value></param><param><value><string>d.get_up_rate=</string></value></param><param><value><string>d.get_down_rate=</string></value></param></params></methodCall>";
my $llen	= length $line;
my $header	= "CONTENT_LENGTH\000${llen}\000SCGI\001\000";
my $hlen	= length $header;
$line		= "${hlen}:${header},${line}";

if ( $src eq "socket" ) {
	socket( SOCK, PF_UNIX, SOCK_STREAM, 0 );
	connect( SOCK, sockaddr_un( $socket ) );
} else {
	socket( SOCK, PF_INET, SOCK_STREAM, getprotobyname( "tcp" ) );
	connect( SOCK, sockaddr_in( $port, inet_aton( $ip ) ) );
}

print SOCK $line;
flush SOCK;

my $up = 0;
my $down = 0;

while ( $line = <SOCK> ) {
	if ( $line =~ /$pattern/ ) {
		$up += $2;
		$line = <SOCK>;
		if ( $line =~ /$pattern/ ) {
			$down += $2;
		}
	}
}

close (SOCK);

print "up.value ${up}\ndown.value ${down}\n";

exit;
